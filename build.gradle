plugins {
    id 'java'
    id 'eclipse'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://dl.cloudsmith.io/public/cinchapi/open-source/maven/"
    }
    maven {
        url "https://dl.cloudsmith.io/public/cinchapi/open-source-snapshots/maven/"
    }
    maven {
        url "https://dl.cloudsmith.io/public/cinchapi/platform/maven/"
    }
    maven {
        url "https://dl.cloudsmith.io/public/cinchapi/platform-snapshots/maven/"
    }
    maven {
        url "https://dl.cloudsmith.io/public/cinchapi/enterprise/maven/"
        credentials {
            username = System.getenv('CLOUDSMITH_API_USER')
            password = System.getenv('CLOUDSMITH_API_KEY')
        }
    }
    maven {
        url "https://dl.cloudsmith.io/public/cinchapi/enterprise-snapshots/maven/"
        credentials {
            username = System.getenv('CLOUDSMITH_API_USER')
            password = System.getenv('CLOUDSMITH_API_KEY')
        }
    }
    maven {
      url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}


// Set the version for all Concourse dependencies
ext.concourseVersion = '0.11.1'

dependencies {
	compile 'com.google.guava:guava:25.1-jre'
	compile (group: 'com.cinchapi', name: 'accent4j', version: '1.12.1', changing:true) {
        force = true
    }
	compile group: 'com.cinchapi', name: 'concourse-driver-java', version: concourseVersion, changing:true
  	compile group: 'com.cinchapi', name: 'concourse-import', version: concourseVersion, changing:true
	compile group: 'com.cinchapi', name: 'concourse-plugin-core', version: concourseVersion, changing:true
	compile 'org.slf4j:slf4j-api:1.7.5'
	compile 'ch.qos.logback:logback-classic:1.0.13'
	compile 'com.google.code.gson:gson:2.2.4'
	compile 'net.sf.trove4j:trove4j:3.0.3'
	compile 'com.github.stephenc.high-scale-lib:high-scale-lib:1.1.4'
	compile ('org.reflections:reflections:0.9.9-RC2'){
        exclude group: 'org.javassist', module:'javassist'
    }
    compile 'org.javassist:javassist:3.18.2-GA'

	testCompile 'junit:junit:4.11'
	testCompile group: 'com.cinchapi', name: 'concourse-ete-test-core', version: concourseVersion, changing:true
}

def getVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'bash', 'version.sh'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

group = 'com.cinchapi'
version = getVersion()
// Drop the build component from version number and use that for
// publishing
ext.mavenVersion = version.split('\\.')
ext.mavenVersion[3] = ext.mavenVersion[3].replaceAll("[0-9]+-", "-")
ext.mavenVersion[3] = ext.mavenVersion[3].replaceAll("[0-9]+", "").trim()
ext.mavenVersion = ext.mavenVersion.join(".").replace(".-", "-").replaceAll('\\.$', "")

jar {
    manifest {
        attributes("Specificiation-Title": "Runway", "Specificiation-Version": version, "Implementation-Version": version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
  repositories {
    maven {
      name = 'cloudsmith'
      def releasesRepoUrl = "https://api-g.cloudsmith.io/maven/cinchapi/open-source/"
      def snapshotsRepoUrl = "https://api-g.cloudsmith.io/maven/cinchapi/open-source-snapshots/"
      url = mavenVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = System.getenv('CLOUDSMITH_API_USER')
        password = System.getenv('CLOUDSMITH_API_KEY')
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId group
      artifactId project.name
      version mavenVersion
    }
  }
}

test {
    testLogging {
        showStandardStreams = true
    }
}
